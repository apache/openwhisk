ext.dockerImageName = "whisk/go-cli"
ext.dockerContainerName = "go-cli"
apply from: '../../gradle/docker.gradle'


task removeBinary(type: Delete) {
    delete "${projectDir}/../../bin/go-cli"
}

task distBinary(dependsOn: [removeBinary, distDocker]) << {
    run(dockerBinary + ["rm", "-f", dockerContainerName], true)
    run(dockerBinary + ["run", "--name", dockerContainerName, dockerRegistry + dockerImageName + ":" + dockerImageTag])

    // Copy all Go binaries from Docker into openwhisk/bin/go-cli folder
    run(dockerBinary + ["cp", dockerContainerName +
            ":/src/github.com/go-whisk-cli/build/.", "${projectDir}/../../bin/go-cli"])

    run(dockerBinary + ["rm", "-f", dockerContainerName])
}

task dumpOSInfo {
    println "os.name = "+getOsName()
    println "os.arch = "+getOsArch()
    println "go.name = "+mapOsNameToGoName(getOsName())
    println "go.arch = "+mapOsArchToGoArch(getOsArch())
}

task copyCLIShortcut(type: Copy, dependsOn: [distBinary, dumpOSInfo]) {
    String go_osname     = mapOsNameToGoName(getOsName())
    String go_osarch     = mapOsArchToGoArch(getOsArch())
    String from_path_wsk = "${projectDir}/../../bin/go-cli/${go_osname}/${go_osarch}/wsk"
    String to_path_dir   = "${projectDir}/../../bin/go-cli"

    from from_path_wsk
    into to_path_dir
 }



distDocker.finalizedBy copyCLIShortcut

// Returns the Go CLI docker build args
def getDockerBuildArgs() {
    java.text.SimpleDateFormat sdf = new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssZ")
    String envDateStr = "$System.env.WHISK_BUILD_TIMESTAMP"
    String dateStr = ((envDateStr != "null") ? envDateStr : sdf.format(new Date()))
    return "BUILDTIME="+dateStr
}

def run(cmd, ignoreError = false) {
    println("Executing '${cmd.join(" ")}'")
    def proc = cmd.execute()
    proc.waitFor()
    if(!ignoreError && proc.exitValue() != 0) {
        println("Command '${cmd.join(" ")}' failed with exitCode ${proc.exitValue()}")
    }
}

def getOsName() {
    return System.properties['os.name']
}

def getOsArch() {
    return System.properties['os.arch']
}

def mapOsNameToGoName(String osname) {
    String osname_l = osname.toLowerCase()
    if (osname_l.contains("nux") || osname.contains("nix")) return "linux"
    if (osname_l.contains("mac")) return "mac"
    if (osname_l.contains("windows")) return "windows"
    return osname_l
}

def mapOsArchToGoArch(String osarch) {
    String osarch_l = osarch.toLowerCase()
    if (osarch_l.contains("x86_64") || osarch_l == "amd64") return "amd64"
    if (osarch_l.contains("i386") || osarch_l.contains("x86_32")) return "386"
    return osarch_l
}
